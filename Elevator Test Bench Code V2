LIBRARY IEEE;
USE IEEE.STD_LOGIC_1164.ALL;
USE IEEE.NUMERIC_STD.ALL;

ENTITY tbElevator IS
END tbElevator;

ARCHITECTURE Behavioral of tbElevator IS

    signal clk : std_logic := '0';
    signal HardReset : std_logic := '0';
    signal SoftReset : std_logic := '0';
    signal estop : std_logic := '0';
    signal tick : std_logic := '0';
    signal ReqButtons : std_logic_vector(3 downto 0) := (others => '0');
    
    signal CurrentFloor : integer;
    signal DoorOpen, MovingUp, MovingDown : std_logic;
    signal debugger : std_logic_vector(2 downto 0);

    constant CLKPERIOD : time := 20 ns;

BEGIN

    UUT: ENTITY work.ElevatorController
        generic map(
            NumFloors => 4,
            TravelTime => 2,
            DoorTime => 2 
        )
        port map (
            clk => clk,
            HardReset => HardReset,
            SoftReset => SoftReset,
            estop => estop,
            tick => tick,
            ReqButtons => ReqButtons,
            CurrentFloor => CurrentFloor,
            DoorOpen => DoorOpen,
            MovingUp => MovingUp,
            MovingDown => MovingDown,
            debugger => debugger
        );

    -- Clock Generation
    clk_process: process
    BEGIN
        clk <= '0'; wait for CLKPERIOD/2;
        clk <= '1'; wait for CLKPERIOD/2;
    END process;

    tick_process: process
    BEGIN
        wait for CLKPERIOD * 10;
        tick <= '1';
        wait for CLKPERIOD;
        tick <= '0';
    END process;
	 
	 
	 
    stim_proc: process
    BEGIN
        -- Reset System
        HardReset <= '1';
        wait for 100 ns;
        HardReset <= '0';
        wait for 100 ns;
        
        report "Test 1: Request Floor 2";
        ReqButtons(2) <= '1';
        wait for CLKPERIOD * 2;
        ReqButtons(2) <= '0';
        
		  
        wait until DoorOpen = '1';
        assert CurrentFloor = 2 report "Failed: Did not arrive at floor 2" severity failure;
        report "Success: Arrived at Floor 2";

        wait until DoorOpen = '0'; 
        wait for 200 ns;

        ReqButtons(0) <= '1';
        wait for CLKPERIOD * 20; -- Wait until start moving down
        ReqButtons(0) <= '0';
        
		  -- Mid Motion Request in between floors
        ReqButtons(1) <= '1'; 
        wait for CLKPERIOD * 2;
        ReqButtons(1) <= '0';
        
        wait until DoorOpen = '1';
        assert CurrentFloor = 1 report "Failed: Skipped Floor 1" severity error;
        report "Success: Stopped at intermediate Floor 1";
        
        wait until DoorOpen = '0';

        wait until DoorOpen = '1';
        assert CurrentFloor = 0 report "Failed: Did not continue to Floor 0" severity error;

        report "Test 3: Soft Reset";
        -- Request Floor 3
        ReqButtons(3) <= '1';
        wait for CLKPERIOD * 5;
        ReqButtons(3) <= '0';
        
        -- Assert Soft Reset
        SoftReset <= '1';
        wait for CLKPERIOD * 5;
        SoftReset <= '0';
        
        -- System shouldnt move to 3 because request was cleared.
        wait for 1000 ns;
        assert CurrentFloor = 0 report "Failed: Moved despite Soft Reset" severity error;
        
        report "Test 4: ESTOP";
        -- Request Floor 3 again
        ReqButtons(3) <= '1';
        wait for CLKPERIOD * 2;
        ReqButtons(3) <= '0';
        
        wait until MovingUp = '1';
        estop <= '1';
        wait for CLKPERIOD * 5;
        assert MovingUp = '0' report "Failed: Did not halt on ESTOP" severity error;
        
        estop <= '0';
        
        report "All tests completed.";
        wait;
    END process;

END Behavioral;
